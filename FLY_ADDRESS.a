FLY_ADDRESS:
					CALL 		LCD_SHO						; SHOW THE LAST ALPHABET OF TGHE OPCODE (DISPLAY THE CHARATER IN 'A' FROM THE CALLER PROGRAM)
FLY_ADDRESS_SKIP:
					CALL		KEYPAD_ID					; ENSURE A KEY IS DEPRESSED
					JNB			01H, FLY_ADDRESS_SKIP
					CLR			01H							; GET READY FOR NEXT KEY

					CJNE		A, #'*', FLY_ADDRESS_SKIP	; ENSURES NEXT IS USE TO GIVE SPACE
					MOV			A, #' '						; GIVE A GAP				   
					CALL 		LCD_SHO
					CLR			03H							; SELECT OPCODE TYPE IN THE 'NEXT SR'
					CALL		NEXT						; CALL NEXT OSR TO PERMANENTLY SAVE THE OPCODE'S CODES
FLY_SUB_MEM:					
					MOV			32H, #04H					; ENSURES THAT ONLY FOUR NIBBLES (ie 2 BYTE DATA) ARE ENTERED
					SETB		04H							; SET THE BYTE INDICATOR FOR A 2 BYTE(4 NIBBLE) OPERAND CONVERSION
					SETB		06H							; SET FLAG TO CAUSE THE ASCII_2_HEX SR TO CARRY OUT HIGH ORDER BYTE MARRIAGE FIRST

FA_1:				CALL		KEYPAD_ID					; ENSURE A KEY IS DEPRESSED
					JNB			01H, FA_1
					CLR			01H							; GET READY FOR NEXT KEY

					JNB			08H, FA_CONTINUE			; IF IT IS NOT SUB MEM PASSING THIS SEGMENT SKIP THE SEGMENT
					CLR			05H							; REFRESH THE BYTE MARRIAGE FLAG. IT HELPS THE CANCEL SR
					CJNE		A, #'N', FA_CONTINUE		; ELOO NOTE THAT THE FOLLOWING 3 INSTRUCTION IS SOLELY FOR SUB MEM'S CANCEL
					CLR_LCD
					SJMP		FLY_SUB_MEM

FA_CONTINUE:		CALL		ASCII_2_HEX					; DO THE CONVERSION AND COMPARE
					JNB			02H, FA_1					; ENSURES THAT THE KEY PRESSED IS BETWEEN 0 - F IN HEX
					CLR			02H							; GET READY FOR NEXT KEY

					MOV 		A,31H						; DISPLAY THE NUMBER
					CALL		LCD_SHO
					DJNZ		32H, FA_1					; CHECK IF FOUR NIBBLES (TWO BYTES) DATA HAVE BEEN ENTERED

FA_2:				CALL		KEYPAD_ID					; ENSURE A KEY IS DEPRESSED
					JNB			01H, FA_2
					CLR			01H							; GET READY FOR NEXT KEY
					CJNE		A, #'*', FA_2				; ENSURES NEXT IS PRESSED TO CONTINUE

					JB			00H, FA_EXIT				; SKIP THE SR SEGMENT BELOW IF IT IS A SUB MEM CALL

					SETB		03H							; SELECT OPERAND TYPE IN THE 'NEXT SR' (NOT OPCODE TYPE)
					SETB		07H							; SELECT ADDRESS TYPE OPERAND IN THE 'NEXT SR' FOR 8085 ONLY
					CALL		NEXT						; CALL NEXT OSR TO PERMANENTLY SAVE THE OPERAND CODES

					MOV			43H, 3AH					; SAVE THE CURRENT ADDRESS IN THE PAGE MEMORY (HOB)
					MOV			42H, 39H					;											  (LOB)

					CALL		ADDRESS_DISPLAY_CONTROL		; CALL THIS OSR TO CLEAR THE LCD SCREEN AND WRITE THE NEW ADDRESS IN LINE 1
FA_EXIT:																		   
					RET