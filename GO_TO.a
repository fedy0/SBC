; GO_TO (GO TO)
; THIS A SR THE SELECTS OR GIVE THE USER THE OPPORTUNITY TO EXAMINE A PARTICULAR PART OF A PROGRAM
; USE UNDER THE V_HEX(VIEW HEX) MODE ONLY

GO_TO: 
					CLR			IE.7						; DISABLE THE EXTERNAL INTERRUPT(!INT0) USING THE GLOBAL IE BIT
					CLR			05H							; REFRESH THE BYTE MARRIAGE FLAG. IT HELPS THE CANCEL SR

					CLR_LCD									; CLEAR THE LCD SCREEN
					D_STR									; DISPLAY THE FOLLOWING STRINGS
					DB			'GO TO: ',0

					MOV			32H, #04H					; ENSURES THAT ONLY FOUR NIBBLES (ie 2 BYTE DATA) ARE ENTERED
					SETB		04H							; SET THE BYTE INDICATOR FOR A 2 BYTE(4 NIBBLE) ADDRESS CONVERSION
					SETB		06H							; SET FLAG TO CAUSE THE ASCII_2_HEX SR TO CARRY OUT HIGH ORDER BYTE MARRIAGE FIRST

GT_1:				CALL 		KEYPAD_ID  					; ENSURE THAT A KEY IS PRESSED
					JNB			01H, GT_1
					CLR			01H							; GET READY FOR THE NEXT KEY

					CJNE		A, #'N', GT_2				; ELOO NOTE THAT THE FOLLOWING 3 INSTRUCTION IS SOLELY FOR GOTO'S CANCEL
; IF 'N' IS PRESSED (ie CANCEL) RETURN TO THE CALL PROGRAM
					SJMP		EXIT_GOTO					; ESCAPE THE OSR(GOTO)

GT_2:				CALL		ASCII_2_HEX					; DO THE CONVERSION AND COMPARE
					JNB			02H, GT_1					; ENSURES THAT THE KEY PRESSED IS BETWEEN 0 - F IN HEX
					CLR			02H							; GET READY FOR NEXT KEY

					MOV 		A,31H						; DISPLAY THE NUMBER
					CALL		LCD_SHO
					DJNZ		32H, GT_1					; CHECK IF FOUR NIBBLES (TWO BYTES) DATA HAVE BEEN ENTERED

GT_3:				CALL		KEYPAD_ID					; ENSURE A KEY IS DEPRESSED
					JNB			01H, GT_3
					CLR			01H							; GET READY FOR NEXT KEY
					
; WAIT FOR NEXT OR CANCEL
					CJNE		A, #'N', GT_4				; ENSURES NEXT IS PRESSED TO CONTINUE OR 'N' TO ESCAPE THE OSR
					SJMP		EXIT_GOTO					; ESCAPE THE OSR(GOTO)

GT_4:				CJNE		A, #'*', GT_3			    ; ACCEPT USER'S INPUT ADDRESS FOR TEST

; COMPARE ADDRESS WITH THE START ADDRESS MEM LOCS.
					MOV			A, 3FH						; LOAD THE START ADDRESS HOB FOR COMPARISON	WITH GOTO'S HOB ADDRESS
					CJNE		A, 35H, GT_5
					SJMP		GT_6						; IF EQUAL JUMP TO TEST THE LOB(THIS IS DONE ONLY FOR EQUALITY ENCOUTERED)
GT_5:				JNC			OUT_OF_RANGE				; EXIT IF START ADDRESS HOB IS GREATER THAN THE GOTO'S HOB ADDRESS
					SJMP		GT_8						; SKIP LOB ADDRESS TEST, CONTINUE WITH THE END ADDRESS TEST

GT_6:				MOV			A, 3EH						; LOAD THE START ADDRESS LOB FOR COMPARISON	WITH GOTO'S LOB ADDRESS
					CJNE		A, 34H, GT_7
					SETB		C
GT_7:				JNC			OUT_OF_RANGE				; EXIT IF START ADDRESS LOB IS GREATER THAN THE GOTO'S LOB ADDRESS 
															; ELSE CONTINUE
; COMPARE ADDRESS WITH THE END ADDRESS MEM LOCS.
GT_8:				MOV			A, 41H						; LOAD THE END ADDRESS HOB FOR COMPARISON WITH GOTO'S HOB ADDRESS
					CJNE		A, 35H, GT_9
					SJMP		GT_10						; IF EQUAL JUMP TO TEST THE LOB(THIS IS DONE ONLY FOR EQUALITY ENCOUTERED)
GT_9:				JC			OUT_OF_RANGE				; EXIT IF END ADDRESS HOB IS GREATER THAN THE GOTO'S HOB ADDRESS
					SJMP		GT_12						; SKIP LOB ADDRESS TEST, AND CARRY OUT A JUMP TO THE USER'S DESIRED ADDRESS

GT_10:				MOV			A, 40H						; LOAD THE END ADDRESS LOB FOR COMPARISON WITH GOTO'S LOB ADDRESS
					CJNE		A, 34H, GT_11
					CLR			C
GT_11:				JC			OUT_OF_RANGE				; EXIT IF END ADDRESS LOB IS GREATER THAN THE GOTO'S LOB ADDRESS ELSE CONTINUE
; ALL TESTS HAVE BEEN COMPLETED
															; ACCEPT USER'S INPUT ADDRESS AND EXIT
GT_12:				MOV			3DH, 35H					; LOAD THE USER'S DESIRED ADDRESS(HOB) INTO THE V_HEX CURRENT ADDRESS MEM LOC.2(HOB)
					MOV			3CH, 34H					; LOAD THE USER'S DESIRED ADDRESS(LOB) INTO THE V_HEX CURRENT ADDRESS MEM LOC.1(LOB)
					SJMP		EXIT_GOTO
OUT_OF_RANGE:			
					CLR_LCD									; CLEAR THE LCD SCREEN
					D_STR									; DISPLAY THE FOLLOWING STRINGS
					DB			'  Out Of Range  ',0
					CALL		DELAY_2S					; DELAY FOR THE USER TO SEE THE MESSAGE
					AJMP		GO_TO						; START AFRESH
EXIT_GOTO:
					SETB		IE.7						; NOW ENABLE THE EXTERNAL INTERRUPT
					RET
		; IF THE ADDRESS PUT BY THE USER PASS THE TEST OF COMPARISON LOAD THE APPROPRIATE MEM LOCS WITH THE ADDRESS AND EXIT THE OSR
		; ELSE DISPLAY THE FOLOWING MESSAGE: 'OUT OF RANGE' FOR TWO SECONDS (USE 1SEC X 2) THEN JUMP BACK TO BLANK THE LCD SCREEN